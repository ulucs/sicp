;here comes the derivative
(define (deriv expr var)
  (cond ((number? expr) 0)
        ((variable? expr)
         (if (same-variable? expr var)
           1 0))
        ((sum? expr)
         (make-sum (deriv (addend expr) var)
                   (deriv (augend expr) var)))
        ((product? expr)
         (make-sum
           (make-product (multiplier expr)
                         (deriv (multiplicand expr) var))
           (make-product (deriv (multiplier expr) var)
                         (multiplicand expr))))
        ((power? expr)
           (make-product
             (make-product (pow expr)
                           (make-power (base expr)
                                       (- (pow expr) 1)))
             (deriv (base expr) var)))
        (else
          (error "unknown expression type -- DERIV" expr))))

;yes, this does not exist
(define (=number? a b)
  (and (number? a)
       (eq? a b)))

;and the helper functions
(define variable? symbol?)
(define (same-variable? a b)
  (and
    (variable? a)
    (variable? b)
    (eq? a b)))

(define (make-sum v w)
  (cond ((=number? v 0) w)
        ((=number? w 0) v)
        ((and (number? v)
              (number? w)) (+ v w))
        (else (list '+ v w))))
(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))
(define addend cadr)
(define augend caddr)

(define (make-product v w)
  (cond ((=number? v 0) 0)
        ((=number? v 1) w)
        ((=number? w 0) 0)
        ((=number? w 1) v)
        ((and (number? v)
              (number? w)) (* v w))
        (else (list '* v w))))
(define (product? x)
  (and (pair? x) (eq? (car x) '*)))
(define multiplier cadr)
(define multiplicand caddr)

(define (make-power x n)
  (cond ((=number? 0 n) 1)
        ((=number? 0 x) 0)
        ((=number? 1 n) x)
        ((=number? 1 x) 1)
        ((and (number? x)
              (number? n)) (expt x n))
        (else (list '** x n))))
(define (power? expr)
  (and (pair? expr) (eq? (car expr) '**)))
(define pow caddr)
(define base cadr)

(deriv (make-power 'x 5) 'x)
(deriv (make-power 'y 5) 'x)
(deriv (make-power 'x 0) 'x)
(deriv (make-power 1 5) 'x)
(deriv (make-power 'x 1) 'x)
(deriv (make-power 'x 2) 'x)
